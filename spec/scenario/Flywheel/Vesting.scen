Macro FlywheelComptroller price=1.0 borrowRate=0.000005 compInitAmount=5000000e18
    Unitroller Deploy
    PriceOracle Deploy Fixed price
    PriceOracleProxy Deploy Admin (PriceOracle Address) (Address Zero) (Address Zero) (Address Zero) (Address Zero) (Address Zero)
    ----g2
    ComptrollerImpl Deploy ScenarioG2 ComptrollerScenG2
    Unitroller SetPendingImpl ComptrollerScenG2
    ComptrollerImpl ComptrollerScenG2 BecomeG2
    --list some tokens
    Comptroller SetPriceOracle (PriceOracleProxy Address)
    Comptroller SetMaxAssets 20
    Comptroller SetCloseFactor 0.5
    Comptroller LiquidationIncentive 1.1
    NewCToken ZRX cZRX
    NewCToken BAT cBAT
    Support cZRX collateralFactor:0.5
    Support cBAT collateralFactor:0.5
    -- final
    ComptrollerImpl Deploy ScenarioG3 ComptrollerScen
    Unitroller SetPendingImpl ComptrollerScen
    ComptrollerImpl ComptrollerScen BecomeG3 1e18 [cZRX cBAT]
    Erc20 Deploy Standard COMP "COMP Token" 18
    Give (Address Comptroller) compInitAmount COMP

Macro VestingComptroller
    FlywheelComptroller
    -- g4
    ComptrollerImpl Deploy ScenarioG4 ComptrollerScen
    Unitroller SetPendingImpl ComptrollerScen
    ComptrollerImpl ComptrollerScen BecomeG4
    -- g5
    ComptrollerImpl Deploy ScenarioG5 ComptrollerScen
    Unitroller SetPendingImpl ComptrollerScen
    ComptrollerImpl ComptrollerScen BecomeG5
    -- current
    ComptrollerImpl Deploy Scenario ComptrollerScen
    Unitroller SetPendingImpl ComptrollerScen
    ComptrollerImpl ComptrollerScen Become 2000

Macro InitSpeeds
    Prep Geoff 100e18 ZRX cZRX
    Mint Geoff 50e18 cZRX--tokenbalance = 50e18 / 2e9 = 2.5e10
    Prep Coburn Some BAT cBAT
    Mint Coburn 6e18 cBAT--tokenbalance = 6e18 / 2e9 = 3e9
    EnterMarkets Coburn cBAT
    Borrow Coburn 1e18 cZRX
    Comptroller RefreshCompSpeeds

Macro InitVestingSpeeds
    InitSpeeds
    Comptroller SetVestingPeriod 2000
    Comptroller Send "setCompAddress(address)" (Address COMP)

Test "Vesting COMP happens at the same rate as the flywheel"
    VestingComptroller
    InitVestingSpeeds
    -- first 2000 blocks 
    FastForward 2000 Blocks
    Comptroller ClaimComp Geoff
    Assert Equal (Comptroller CompAccrued Geoff) 0
    Assert Equal (Erc20 COMP TokenBalance Geoff) 2e21
    -- second 2000 blocks
    FastForward 2000 Blocks
    Comptroller ClaimComp Geoff
    Assert Equal (Comptroller CompAccrued Geoff) 0
    Assert Equal (Erc20 COMP TokenBalance Geoff) 4e21
    -- testing that velocity unchanged from Flywheel.scen (get up to 300000 blocks, same as flywheel)
    FastForward 296000 Blocks
    Comptroller ClaimComp Geoff
    Assert Equal (Comptroller CompAccrued Geoff) 0
    Assert Equal (Erc20 COMP TokenBalance Geoff) 3e23

Test "No COMP can be accrued ahead of vesting"
    VestingComptroller
    InitVestingSpeeds
    -- first 1999 blocks
    FastForward 1999 Blocks
    Comptroller ClaimComp Geoff
    Assert Equal (Comptroller CompAccrued Geoff) 0
    Assert Equal (Erc20 COMP TokenBalance Geoff) 0
    -- 1 more block
    FastForward 1 Blocks
    Comptroller ClaimComp Geoff
    Assert Equal (Comptroller CompAccrued Geoff) 0
    Assert Equal (Erc20 COMP TokenBalance Geoff) 2e21
    -- half blocks
    FastForward 1000 Blocks
    Comptroller ClaimComp Geoff
    Assert Equal (Comptroller CompAccrued Geoff) 0
    Assert Equal (Erc20 COMP TokenBalance Geoff) 2e21
    -- go past the vesting point
    FastForward 2000 Blocks
    Comptroller ClaimComp Geoff
    Assert Equal (Comptroller CompAccrued Geoff) 0
    Assert Equal (Erc20 COMP TokenBalance Geoff) 4e21
    -- reach final vest
    FastForward 1000 Blocks
    Comptroller ClaimComp Geoff
    Assert Equal (Comptroller CompAccrued Geoff) 0
    Assert Equal (Erc20 COMP TokenBalance Geoff) 6e21

Test "Double vesting cannot happen"
    VestingComptroller
    InitVestingSpeeds
    Assert Equal (Comptroller CompAccrued Coburn) 0
    -- first 2000 blocks
    FastForward 2000 Blocks
    Comptroller ClaimComp Coburn
    Assert Equal (Comptroller CompAccrued Coburn) 0
    Assert Equal (Erc20 COMP TokenBalance Coburn) 2e21
    -- second 2000 blocks
    FastForward 2000 Blocks
    Comptroller ClaimComp Coburn
    Assert Equal (Comptroller CompAccrued Coburn) 0
    Assert Equal (Erc20 COMP TokenBalance Coburn) 4e21
    -- testing that velocity unchanged from Flywheel.scen (get up to 300000 blocks, same as flywheel)
    FastForward 296000 Blocks
    Comptroller ClaimComp Coburn
    Assert Equal (Comptroller CompAccrued Coburn) 0
    Assert Equal (Erc20 COMP TokenBalance Coburn) 3e23
